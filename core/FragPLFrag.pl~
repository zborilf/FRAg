

%
%	FRAg clauses for late bindings
%	2021 - 2022
%	Frantisek Zboril
%





%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%	Reasoning methods - intention, plan and substitution selections
%



% reasoning - plan selection + substitution selection (decide op.) + intention selection
:-thread_local active_intention_selection /1.
:-thread_local active_plan_selection /1.
:-thread_local active_substitution_selection /1.

:-dynamic default_intention_selection /1.
:-dynamic default_plan_selection /1.
:-dynamic default_substitution_selection /1.


set_intention_selection(Intention_Selection):-
    reasoning_method(Intention_Selection),				% checks if such reasoning exists
    retractall(active_intention_selection( _ )),
    assert(active_intention_selection(Intention_Selection)),
    format(atom(String),
           "[SYSDBG] Setting intention selection method to ~w~n",
           [Intention_Selection]),
    println_debug(String, systemdbg).


set_intention_selection(Intention_Selection):-
    format(atom(String), "Intention selection cannot be switched to ~w~n",
	   [Intention_Selection]),
    print_debug(String, error).


set_default_intention_selection(Intention_Selection):-
    reasoning_method(Intention_Selection),
    retractall(default_intention_selection( _ )),
    assert(default_intention_selection(Intention_Selection)),
    format(atom(String),
           "[SYSDBG] Setting default intention selection method to ~w~n",
           [Intention_Selection]),
    println_debug(String, systemdbg).

set_default_intention_selection(Intention_Selection):-
    format(atom(STRING),
           "Default intention selection cannot be switched to ~w~n",
           [Intention_Selection]),
    print_debug(STRING, error).


set_plan_selection(Plan_Selection):-
    reasoning_method(Plan_Selection),
    retractall(active_plan_selection( _ )),
    assert(active_plan_selection(Plan_Selection)),
    format(atom(String), "[SYSDBG] Setting plan selection method to ~w~n",
           [Plan_Selection]),
    println_debug(String, systemdbg).


set_plan_selection(Plan_Selection):-
    format(atom(String), "[ERROR] Plan selection cannot be switched to ~w~n",
	       [Plan_Selection]),
    println_debug(String, error).

set_default_plan_selection(Plan_Selection):-
    reasoning_method(Plan_Selection),
    retractall(default_plan_selection( _ )),
    assert(default_plan_selection(Plan_Selection)),
    format(atom(String), "[SYSDBG] Setting default plan selection method to ~w~n",
                [Plan_Selection]),
    println_debug(String, systemdbg).

set_default_plan_selection(Plan_Selection):-
    format(atom(String),
           "[ERROR] Default plan selection cannot be switched to ~w~n",
           [Plan_Selection]),
    println_debug(String, error).


set_substitution_selection(Substitution_Selection):-
    reasoning_method(Substitution_Selection),
    retractall(active_substitution_selection( _ )),
    assert(active_substitution_selection(Substitution_Selection)),
    format(atom(String), "[SYSDBG] Setting substitution selection method to ~w~n",
           [Substitution_Selection]),
    println_debug(String, systemdbg).


set_substitution_selection(Substitution_Selection):-
    format(atom(String),
	   "[ERROR] Substitution selection cannot be switched to ~w~n",
	   [Substitution_Selection]),
    print_debug(String, error).


set_default_substitution_selection(Substitution_Selection):-
    reasoning_method(Substitution_Selection),
    retractall(default_substitution_selection( _ )),
    assert(default_substitution_selection(Substitution_Selection)),
    format(atom(String), "[SYSDBG] Setting default decision method to ~w~n",
           [Substitution_Selection]),
    println_debug(String, systemdbg).

set_default_substitution_selection(Substitution_Selection):-
    format(atom(String),
           "[ERROR] Default substitution selection cannot be switched to ~w~n",
           [Substitution_Selection]),
    print_debug(String, error).



set_reasoning(Reasoning):-
    reasoning_method(Reasoning),
    set_intention_selection(Reasoning),
    set_plan_selection(Reasoning),
    set_substitution_selection(Reasoning).

set_reasoning(Reasoning):-
    format(atom(String), "[ERROR] Reasoning cannot be switched to ~w~n",
           [Reasoning]),
    print_debug(String, error).



set_default_reasoning(Reasoning):-
    reasoning_method(Reasoning),
    set_default_intention_selection(Reasoning),
    set_default_plan_selection(Reasoning),
    set_default_substitution_selection(Reasoning).

set_default_reasoning(Reasoning):-
    format(atom(String),
	   "[ERROR] Default reasoning cannot be switched to ~w~n", [Reasoning]),
    print_debug(String, error).



get_default_reasoning(Intention_Selection, Plan_Selection,
                      Substitution_Selection):-
    default_intention_selection(Intention_Selection),
    default_plan_selection(Plan_Selection),
    default_substitution_selection(Substitution_Selection).


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


update_models2([]).

update_models2([Model| Models]):-
    update_model(Model),			% v jednotlivych reasoninzich
    update_models2(Models).

update_models:-
    active_intention_selection(Intention_Selection),
    active_plan_selection(Plan_Selection),
    active_substitution_selection(Substitution_Selection),
    list_to_set([Intention_Selection, Plan_Selection, Substitution_Selection],
                Models),
    !,
    update_models2(Models).

update_models.




get_intention(Intentions, intention(Intention_ID, Context, Plan_Stack)):-
    active_intention_selection(intention_selection),
    get_intention(intention_selection, Intentions, intention(Intention_ID, Context, Plan_Stack)),
    %	intention(INTENTIONINDEX, CONTEXT, PLANSTACK2),
    loop_number(Loop),
    format(atom(String),
           "[RSNDBG] GET INTENTION [~w / ~w] -> ~w~n",
               [Loop, intention_selection, intention(Intention_ID, Context, Plan_Stack)]),
    print_debug(String, reasoningdbg).



get_intended_means(Means, Event, Intended_Means):-
    active_plan_selection(Plan_Selection),
    get_plan(Plan_Selection, Event, Means, Intended_Means),
    loop_number(Loop),
    format(atom(String),
           "[RSNDBG] GET PLAN [~w / ~w] -> FOR ~w ~n[......] -> PLAN ~w~n",
		   [Loop, Plan_Selection, Event, Intended_Means]),
    print_debug(String, reasoningdbg).


  %  decide_context(action atom, context, variables in action, chosen substitutions)

decide_context( _, _, [], []).		% no vars / nothing to decide

decide_context(Atom, Context, Variables, Context2):-
    active_substitution_selection(Substitution_Selection),
    get_substitution(Substitution_Selection, Atom, Context, Variables, Context2),
    loop_number(Loop),
    format(atom(String),
           "[RSNDBG] GET DECISION [~w / ~w] ->~n[......] FOR ~w ~w ~n[......] DECISION -> ~w~n",
		   [Loop, Substitution_Selection, Atom, Context, Context2]),
	print_debug(String, reasoningdbg).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%	FRAg Methods
%


%
%	BROAD UNIFICATION
%	briadUnification(GOAL,BASE,SUBSTITUTIONSET)
% SUBSTITUTIONSET =
% BU(GOAL,BASE) =def
%     {SUBSTITUTION : BELIEF from BASE, SUBSTITUTION = mgu(GOAL,BELIEF)}
%

remove_renamings([],[]).

remove_renamings([A=B|T],T2):-
    var(A),var(B),
    remove_renamings(T,T2).

remove_renamings([H|T],[H|T2]):-
    remove_renamings(T,T2).

%!  broad_unification(+Atom, +Atoms, +Substitutions)

broad_unification2(_ ,[], []).

broad_unification2(Atom, [Belief| Beliefs], [Substitution2| Substitutions]):-
% SUBSTITUTION = mgu(G,BELIEF)
    unifiable(Atom, Belief, Substitution),
% SUBSTITUTION2 is SUBSTITUTION without renamings
    remove_renamings(Substitution, Substitution2),
% next mgu for the next belief
    broad_unification2(Atom, Beliefs, Substitutions).

broad_unification2(Atom, [_ | Beliefs], Substitutions):-
    broad_unification2(Atom, Beliefs, Substitutions).

broadUnification(GOAL, Beliefs, Substitutions2):-
    broad_unification2(GOAL, Beliefs, Substitutions),
    sort(Substitutions, Substitutions2).

%
%	Reasoning methods (plan / intention / substitution selections)
%



%
%     INSTANCE SET, predicate + context -> list of predicates
%				nesmi se ale zlikvidovat promenne v kontextu
%
% vstupem jsou substituce [[A->a],[B->b] ...] a ty jsou postupne provadeny

apply_substitutions([]).					%

apply_substitutions([Binding| Bindings]):-
    Binding,
    apply_substitutions(Bindings).


instance_set(_ ,[],[]).

instance_set(Atom, [Substitution| Substitutions], Instance_Set):-
    copy_term([Atom| Substitution], [New_Atom| Substitution2]),
    apply_substitutions(Substitution2),
    instance_set(Atom, Substitutions, Substitutions2),
    sort([New_Atom| Substitutions2], Instance_Set).

%
%	SHORT  // neodpovida clanku, tady se z PUS jen vytahnou promenne
%       musi se , vzit promenne, vzit PUS a vytvorit novy PUS a s novymi promennymi
%       pokud mame PUS s prazdnymi unifikatory, udelame z toho jen jedno PUS [[],[],...] -> [[]]
%

set_empty_PUS([[]|_], [[]]).

set_empty_PUS(PUS,PUS).


%	memberVar(A=B,VARS)
%	is A from the variable list VARS?

member_variable(A=_, [C| _]):- A==C.

member_variable(Binding, [_|T]):- member_variable(Binding,T).


delete_binding([], _, []).

delete_binding([A=B| TBINDINGS], C=D, TBINDINGSOUT):-
    A==C, B==D,
    delete_binding(TBINDINGS, C=D, TBINDINGSOUT).

delete_binding([BINDING | TBINDINGS], BINDING2, [BINDING | TBINDINGSOUT]):-
    delete_binding(TBINDINGS, BINDING2, TBINDINGSOUT).


subsubstitution([], _).

subsubstitution(SUBSTITUTION, [BINDING | TBINDINGS]):-
    delete_binding(SUBSTITUTION, BINDING, SUBSTITUTION2),
    subsubstitution(SUBSTITUTION2, TBINDINGS).

%	shorting(SUBSTITUTIONS,VARLIST, S
%	basic shorting: substitution x list of variables -> substitutions just for these variables


shorting([], _, []).	% empty context remains empty context

shorting(_, [], []).	% no variable -> empty context
			% shorts one substitution due to the variables in VARS

% [A->a,B->,D->d,F->d],[A,D] -> [A->a,D->d]
shorting([Binding| Bindings1],Variables,[Binding| Bindings2]):-
    member_variable(Binding, Variables),
    shorting(Bindings1, Variables, Bindings2).

shorting([_|T],VARS,T2):-
    shorting(T,VARS,T2).


% shorts every substitution from the first list with respect with the list of
% variables V (only V-pairs remains)

shorting_PUS([],_,[]).

shorting_PUS([H1|T1],V,[H2|T2]):-
    shorting(H1,V,H2),
    shorting_PUS(T1,V,T2).

%
%	FRAg shorting
%	shorting(vstupnicil1, vystpynicil , vstupni PUS, vstupni promenne, vystupni PUS, vystupni promenne).
%	vstupnicl -> vystupnicil   , ten samy cil ale s radne prejmenovanymi promennymi
%
%	no variables -> empty PUS (bez toho to tuhne, coz by nemelo)
%	shorting(_,_,IPUS,[],[[]],[]).
%

shorting(G,G2,IPUS,IVARS,OPUS,OVARS):-
    shorting_PUS(IPUS,IVARS,SPUS),		% nashortujeme IPUS podle promennych v IV, vysledek v SPUS
    set_empty_PUS(SPUS,SPUS2),			% pokud je vystup [[],[],....] udelame z nej pouze [[]]
    copy_term([G,SPUS2],[G2,OPUS]),		% musime prejmenovat, abychom ziskali 'fresh' jmena promennych
    term_variables(OPUS,OVARS).			% a vezmem jen promenne, ktere jsou v novem PUS  (promennych bylo vic, nez bylo ve vstupnim PUS) shorting [[A=a],[B=b]],[A,C] ->  [[A=a]],[A]


%
%	After execution PUS may include weird tubles constant=constant even for a pair with distinc constants
%	these should be reducet, or better PUS should be reduced only to those mapping variables to a term
%	shortNoVars(PUS, PUS).
%


shortNoVars2([],_,[]).

shortNoVars2([Substitution| Substitutions], Variables, [PUS| PUSs]):-
    shorting(Substitution, Variables, PUS),
    shortNoVars2(Substitutions, Variables, PUSs).

shortNoVars([Substitution| Substitutions], PUSs):-
    term_variables(Substitution, Variables),
    shortNoVars2([Substitution| Substitutions], Variables, PUS),
    sort(PUS, PUSs).


%
%	DECISIONING
%

% PUS , pro mnozinu promennych vybere z jednoho prvku PUS prirazeni, restriction na tyto prirazeni a aplikace



% takes the first PUS from the context / simple reasoning
% selects one substitution from the context due to requestet variebles 'to ground'
% all the substitutions in the context must soud to the variables bindings in the selected context

% uses reasoning method due to active_reasoning_method(-Method)
%



decisioning(ACTIONTERM, CONTEXT, ContextOut):-
    term_variables(ACTIONTERM, ACTIONVARIABLES),
    decide_context(ACTIONTERM, CONTEXT, ACTIONVARIABLES, Context2),
    restrict(CONTEXT, [Context2], ContextOut),
    apply_substitutions(Context2).


%
%	MERGING
%       PUMerged = PU1 m PU2 =def ... see [1]
%	merging(PU1,PU2,PUMerged).
%

appendNE([[]], List, List).
appendNE(List, [[]], List).
appendNE(List1, List2, List3) :- append(List1, List2, List3).

%!  merging3(substitutionsList, substitions) is true, when the same variables in
%
% both substitutions are mapped to the same terms/atoms

merging3([],_).

merging3([A=B|T1],C=D):-
    A==C,!,
    B=D,
    merging3(T1,C=D).

merging3([_|T1],C=D):-
    merging3(T1, C=D).


merging2(_,[]).

merging2(Substitution, [Binding| Bindings]):-
    merging3(Substitution, Binding),
    merging2(Substitution, Bindings).


merging(Substitution1, Substitution2, Substitution_out):-
    merging2(Substitution1, Substitution2),
    append(Substitution1, Substitution2, Substitution3),
    sort(Substitution3, Substitution_out).

merging(_,_,[]).

%
%	RESTRICTION
%	REST(PU1,PU2,RESTRICTION)
%       RESTRICTION = REST(PU1, PU2) =def ... see [1] eq ???.
%

restrict2(_,[],[[]]).


restrict2(PU1,[PU2|TPUS2],PUS):-
    merging(PU1,PU2,PUS2),
    restrict2(PU1,TPUS2,PUS3),
    appendNE([PUS2],PUS3,PUS).


restrict([[]],[[]],[[]]).    % empty PUS's restriction

restrict([H|T], L, PUS):-
    restrict2(H, L, PUS2),
    restrict(T, L, PUS3),
    appendNE(PUS2, PUS3, PUS).

restrict( _, _, []).

%
%	INTERSECTION   (1)
%	goal1,context ~ goal2 =def BU(IS(Goal1,Context),Goal2)
%	intersectionF(Goal1, Context, Goal2, 'Goal1,Context ~ Goal2')
%

intersectionF(G1,CTX,G2,ISEC2):-
    instance_set(G1,CTX,IS),
    broadUnification(G2,IS,ISEC),
    term_variables(G2,G2VARS),
    shorting_PUS(ISEC,G2VARS,ISEC2).

%
%	INTERSECTION   (2)
%       goal1,context ~ goal2, PUS =def REST(BU(IS(goal1,context),goal2),PUS)
%
%

intersectionF(G1,CTX,G2,PUS,ISEC2):-
    intersectionF(G1,CTX,G2,ISEC),
    restrict(ISEC,PUS,ISEC2).

%
%	Queries
%

% it failed
simulate_early_bindings(_, [], []).

% late bindings are set, so do not simulate early bindings
simulate_early_bindings(_, CONTEXT, CONTEXT):-
    late_bindings(true).

simulate_early_bindings(GOAL, CONTEXTIN, CONTEXTOUT):-
    decisioning(GOAL, CONTEXTIN, CONTEXTOUT).





%!  query(+Literal, +Substitutions, -Substitutions)
%  not query
%

query(not(Query), Context_In, Context_Out):-
    %  writeln(query(not(QUERY), CONTEXT, CONTEXTOUT)),
    query(Query, Context_In, Context1),
    % not published yet ... query succeeded, but may be just for some contextes ...
    substract_subsets(Context_In, Context1, Context_Out).

query(not( _ ), Context, Context).   % query to QUERY failed (no answer)


query(Query, Context_In, Context_Out):-
    bagof(fact(Query), fact(Query), Answers),
    broadUnification(fact(Query), Answers, Context1),
    simulate_early_bindings(Query, Context1, Context2),
    restrict(Context_In, Context2, Context_Out).

query( _, _, []).



substract_subsets2([], _, []).

substract_subsets2([Substitution1 | Substitutions], Substitution2, Substitutions_Out):-
    subsubstitution(Substitution1, Substitution2),
    substract_subsets2(Substitutions, Substitution2, Substitutions_Out).

substract_subsets2([SUBSTITUTION | TSUBSTITUTIONS], SUBSTITUTION2, [SUBSTITUTION | SUBSTITUTIONSOUT]):-
    substract_subsets2(TSUBSTITUTIONS, SUBSTITUTION2, SUBSTITUTIONSOUT).


substract_subsets(Substitutions, [], Substitutions).

substract_subsets(PUS, [Substitution | TSUBSTITUTIONS], PUSSUBSTRACTEDOUT):-
    substract_subsets2(PUS, Substitution, PUSSUBSTRACTED),
    substract_subsets(PUSSUBSTRACTED, TSUBSTITUTIONS, PUSSUBSTRACTEDOUT).
